

# Build lib from all source files, except the python bindings where not needed
file(GLOB all_src_files src/*.c src/*.cpp)
if(CMAKE_SYSTEM_PROCESSOR MATCHES "^arm")
	list(REMOVE_ITEM all_src_files "${CMAKE_CURRENT_SOURCE_DIR}/src/pympa.cpp")
endif()

# only need json and pthread unless ION support is enabled in which case we need
# to link to libgbm too which we do on 64-bit qrb5165 libs but not 32-bit
set(LIBS_TO_LINK
	pthread
	modal_json
)

if(EN_ION_BUF)
    if(PLATFORM STREQUAL QCS6490)
		list(APPEND all_src_files src/buffers/dma.cpp)
        list(APPEND LIBS_TO_LINK camxexternalformatutils)
    elseif(PLATFORM STREQUAL QRB5165 OR PLATFORM STREQUAL QRB5165_2)
		list(APPEND LIBS_TO_LINK gbm)
		list(APPEND all_src_files src/buffers/gbm.cpp)
    endif()
endif()

add_library(${LIBNAME} SHARED ${all_src_files})
target_link_libraries(${LIBNAME} LINK_PUBLIC ${LIBS_TO_LINK})

# optional python bindings
set(BUILD_PYMPA ON CACHE BOOL "Whether to build pympa python bindings or not")
if(BUILD_PYMPA)
	message("INFO: Building Python Bindings")
	# Place all Python code in standard distribution directory
	file(GLOB all_python_files ../python/*.py)
	list(APPEND all_python_files ${CMAKE_BINARY_DIR}/python/pympa_types.py)
	install(FILES ${all_python_files} DESTINATION /usr/local/lib/python3.6/dist-packages)

	# Autogenerate Python MPA type definitions
	set(AUTOGEN_PYTHON_OUTPUT ${CMAKE_BINARY_DIR}/python/pympa_types.py)
	set(AUTOGEN_PYTHON_INPUT ${CMAKE_CURRENT_SOURCE_DIR}/include/pipe_interfaces/*.h)
	set(AUTOGEN_PYTHON_FLAGS '-I${CMAKE_CURRENT_SOURCE_DIR}/include')
	add_custom_command(
		OUTPUT ${AUTOGEN_PYTHON_OUTPUT}
		COMMAND mkdir -p ${CMAKE_BINARY_DIR}/python
		COMMAND ctypesgen ${AUTOGEN_PYTHON_FLAGS} ${AUTOGEN_PYTHON_INPUT} -o ${AUTOGEN_PYTHON_OUTPUT}
		DEPENDS ${AUTOGEN_PYTHON_INPUT}
		COMMENT "*** Generating ${AUTOGEN_PYTHON_OUTPUT} ***"
	)
	add_custom_target(
		autogenerated_python_types
		DEPENDS ${AUTOGEN_PYTHON_OUTPUT}
	)
	add_dependencies(${LIBNAME} autogenerated_python_types)
else()
	message("INFO: Skipping Building Python Bindings")
endif()

target_include_directories(${LIBNAME} PUBLIC include )
target_include_directories(${LIBNAME} PUBLIC .) # for mavlink folder

# make the include directory public for install
file(GLOB_RECURSE LIB_HEADERS include/*.h)

set_target_properties(${LIBNAME} PROPERTIES PUBLIC_HEADER "${LIB_HEADERS}")


# make sure everything is installed where we want
# LIB_INSTALL_DIR comes from the parent cmake file


install(
	TARGETS			${LIBNAME}
	LIBRARY			DESTINATION ${LIB_INSTALL_DIR}
	RUNTIME			DESTINATION /usr/bin
	PUBLIC_HEADER	DESTINATION /usr/include
)

install(DIRECTORY include/ DESTINATION /usr/include)
